;********************************
; TALKER_AI.S
;********************************
;	Author:	Patrick Meehan/Mayo "ExoByte" Dylan
;	(c)2000 Interactive Imagination
;	All rights reserved

;********************************
;	BC:		Ptr to gob
?TALKER_AI

	CALL	?BLOCKER_AI
	CALL	?TALKER_AI_CHECK

	AND		A
	RET		Z

	LD		A,(ACTOR_RESTORE_FLAG)
	AND		A
	RET		NZ

	CALL	?ACTOR_SAVE_STATE

	INC		HL
	LD		A,?TALKER_AI_INTERRUPT&$00FF
	LD		(HLI),A
	LD		A,(?TALKER_AI_INTERRUPT>>$08)&$00FF
	LD		(HLI),A
	
	RET

;********************************
;	BC:		Ptr to gob
?TRIGGER_AI

	CALL	?BLOCKER_AI
	CALL	?TALKER_AI_CHECK

	AND		A
	RET		Z

	GET16	H,L,ACTOR_CURRENT
	INC		HL
	LD		A,?TALKER_AI_INTERRUPT&$00FF
	LD		(HLI),A
	LD		A,(?TALKER_AI_INTERRUPT>>$08)&$00FF
	LD		(HLI),A

	RET

;********************************
?TALKER_AI_CHECK

	LDA_FF	AI_HEROFLAGS			; Did the hero press A?
	BIT		HEROFLAGS_ACTION,A
	JP		Z,_FALSE
	
	LD		A,(ACTOR_RESTORE_FLAG)	; Keep multiple talkers from triggering.
	CP		SCRIPT_RESTORE_FREE
	JP		NZ,_FALSE

	LD		A,(TEXTBOX_OPEN)
	CP		TEXTBOX_STATE_CLOSED
	JP		NZ,_FALSE

	LD		A,(SYSTEM_SCRIPT_ACTIVE)
	AND		A
	JP		NZ,_FALSE

	GET16	H,L,ACTOR_CURRENT
	LD		DE,ACTOR_STRUCT_XTILE
	ADD		HL,DE
	LD		A,(HLI)
	LD		C,(HL)
	LD		B,A
;BC HAS ACTOR X,Y
	
	LD		HL,HEROACTOR	
	ADD		HL,DE
	LD		A,(HLI)
	LD		D,A
	LD		E,(HL)
;DE HAS HERO X,Y
	
	;FIRST CHECK FOR SAME TILE
	PUSH	BC
	LD		H,D
	LD		L,E
	TWOS_COMP	B,C
	ADD		HL,BC
	LD		A,H
	OR		L			;16 BIT ADD DOESN'T SET Z FLAG :/
	POP		BC
	JR		Z,_TRUE
	
	LD		A,(HEROACTOR_FLAGS)
	AND		%00000011			;MASK OUT ALL BUT DIR BITS
	
	CP		%00000000
	JR		Z,_UP
	
	CP		%00000001
	JR		Z,_LEFT
	
	CP		%00000010
	JR		Z,_RIGHT

_DOWN
	LD		A,B
	CP		D		;CHECK LEFT EDGE OF BOX
	JR		NZ,_FALSE
	
	ADD		A,0
	CP		D		;CHECK RIGHT EDGE OF BOX
	JR		C,_FALSE
	
	LD		A,C
	SUB		2
	CP		E		;CHECK TOP EDGE OF BOX
	JR		NC,_FALSE
	
	ADD		A,2
	CP		E		;CHECK BOTTOM EDGE OF BOX
	JR		C,_FALSE	

	JR		_TRUE
	
_UP
	LD		A,B
	CP		D		;CHECK LEFT EDGE OF BOX
	JR		NZ,_FALSE
	
	ADD		A,0
	CP		D		;CHECK RIGHT EDGE OF BOX
	JR		C,_FALSE
	
	LD		A,C
	SUB		1
	CP		E		;CHECK BOTTOM EDGE OF BOX
	JR		NC,_FALSE
	
	ADD		A,2
	CP		E		;CHECK TOP EDGE OF BOX
	JR		C,_FALSE	
	
	JR		_TRUE
	
_LEFT
	LD		A,B
	SUB		1
	CP		D		;CHECK LEFT EDGE OF BOX
	JR		NC,_FALSE
	
	ADD		A,2
	CP		D		;CHECK RIGHT EDGE OF BOX
	JR		C,_FALSE
	
	LD		A,C
	ADD		A,1
	CP		E		;CHECK BOTTOM EDGE OF BOX
	JR		C,_FALSE
	
	SUB		3
	CP		E		;CHECK TOP EDGE OF BOX
	JR		NC,_FALSE

	JR		_TRUE
	
_RIGHT
	LD		A,B
	SUB		2
	CP		D		;CHECK LEFT EDGE OF BOX
	JR		NC,_FALSE
	
	ADD		A,2
	CP		D		;CHECK RIGHT EDGE OF BOX
	JR		C,_FALSE

	LD		A,C
	ADD		A,1
	CP		E		;CHECK BOTTOM EDGE OF BOX
	JR		C,_FALSE
	
	SUB		3
	CP		E		;CHECK TOP EDGE OF BOX
	JR		NC,_FALSE
_TRUE
	LD		A,$01
	RET

_FALSE
	XOR		A
	RET


;********************************
?TALKER_AI_INTERRUPT

	ACTOR_OPEN
	
	SET16	H,L,SCRIPT_CURRENT
	
	LD		DE,(ACTOR_STRUCT_INTERRUPT-ACTOR_STRUCT_SCRIPT0)&$FFFF
	ADD		HL,DE
	
	LD		A,(HLI)
	LDFF_A	SCRIPT_WBANK
	LD		A,(HLI)
	LDFF_A	SCRIPT_WFRAME
	LD		A,(HLI)
	LDFF_A	SCRIPT_WFRAME+$01
	
	MOVADDR_FF	SCRIPT_WSTATE,?SCRIPT_START

	SCRIPT_PLAY
	SCRIPT_CLOSE

	SCRIPT_OPEN
	SCRIPT_PLAY
	SCRIPT_CLOSE

	CALL	?BLOCKER_AI_CLEAN_TILE

	COLL_FREEMOVE

	MOVADDR_FF	ACTOR_STATE,?ANIM_AI
	ACTOR_CLOSE

	CALL	?TALKER_AI_HERO

	RET

;********************************
?TALKER_AI_HERO

	XOR			A
	LDFF_A		AI_HEROFLAGS_NEXT
	LDFF_A		AI_HEROFLAGS

	LD			A,(HEROACTOR_FLAGS)
	AND			$03

	CP			ACTOR_FACING_UP
	JR			Z,_UP
	CP			ACTOR_FACING_DOWN
	JR			Z,_DOWN
	CP			ACTOR_FACING_LEFT
	JR			Z,_LEFT
	CP			ACTOR_FACING_RIGHT
	JR			Z,_RIGHT

_UP
	MOVADDR		HEROACTOR_SCRIPT0_FRAME,?_HERO_STAND_UP_ANIM
	JR			_DONE
_DOWN
	MOVADDR		HEROACTOR_SCRIPT0_FRAME,?_HERO_STAND_DOWN_ANIM
	JR			_DONE
_LEFT
	MOVADDR		HEROACTOR_SCRIPT0_FRAME,?_HERO_STAND_LEFT_ANIM
	JR			_DONE
_RIGHT
	MOVADDR		HEROACTOR_SCRIPT0_FRAME,?_HERO_STAND_RIGHT_ANIM

_DONE
	LD			A,:?_HERO_STAND_UP_ANIM
	LD			(HEROACTOR_SCRIPT0_BANK),A
	MOVADDR		HEROACTOR_SCRIPT0_STATE,?SCRIPT_PLAY_NEXT

	MOVADDR		HEROACTOR_SCRIPT1_STATE,?CMD_END

	MOVADDR		HEROACTOR_STATE,?ANIM_AI

	RET

;********************************
	END
;********************************