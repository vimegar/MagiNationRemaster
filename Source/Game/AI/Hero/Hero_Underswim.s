;********************************
; HERO_UNDERSWIM.S
;********************************
;	Author:	Patrick Meehan/Erik Hutchinson
;	(c)2000	Interactive Imagination
;	All rights reserved

;********************************
?HERO_AI_UNDERSWIM

	BATTERY_SET_BANK	RAMB_GAMESTATE
	BATTERY_ON

	LD					A,(XRAM_HERO_ABILITY)
	LD					(HERO_ABILITY),A

	BATTERY_OFF
	JP					?HERO_AI_UNDERSWIM_START

;********************************
;	BC:		Ptr to gob
?HERO_AI_FLOAT
	MOVADDR_FF	SCRIPT_WSTATE,?SCRIPT_START
	MOVADDR_FF	ACTOR_STATE,?HERO_AI_UNDERSWIM_START
	JP			?HERO_AI_CLOSE

?HERO_AI_UNDERSWIM_START
	CALL		?RANDOM_ENCOUNTER
	CALL		?HERO_AI_OPEN

	LD			A,(AI_CNT1)
	BIT			BitRight,A
	JP			NZ,?HERO_AI_UNDERSWIM_RIGHT
	BIT			BitLeft,A
	JP			NZ,?HERO_AI_UNDERSWIM_LEFT
	BIT			BitUp,A
	JP			NZ,?HERO_AI_UNDERSWIM_UP
	BIT			BitDown,A
	JP			NZ,?HERO_AI_UNDERSWIM_DOWN

	; MOVE
	;--------------------------------
	CALL		?HERO_CHECK_CURRENTS
	COLL_DETECT
	CHECK_HOTSPOT
	CALL		?CAMERA_UPDATE

	; CHECK ACTION
	;--------------------------------
	LD			A,(AI_CNTDOWN)
	BIT			BitA,A
	CALL		NZ,?HERO_ACTION_FLOAT

	JP			?HERO_AI_CLOSE

;********************************
?HERO_ACTION_FLOAT

	LDA_FF		ACTOR_FLAGS
	AND			$03

	CP			ACTOR_FACING_UP
	JR			Z,_UP
	CP			ACTOR_FACING_DOWN
	JR			Z,_DOWN
	CP			ACTOR_FACING_LEFT
	JR			Z,_LEFT
	CP			ACTOR_FACING_RIGHT
	JR			Z,_RIGHT

_UP
	JP			?HERO_CHECK_ACTION_UP
_DOWN
	JP			?HERO_CHECK_ACTION_DOWN
_LEFT
	JP			?HERO_CHECK_ACTION_LEFT
_RIGHT
	JP			?HERO_CHECK_ACTION_RIGHT

	RET

;********************************
;	BC:		Ptr to gob
?HERO_AI_UNDERSWIM_DOWN
	LD			A,%00000011
	LDFF_A		ACTOR_FLAGS
	MOVADDR_FF	SCRIPT_WSTATE,?SCRIPT_START
	LD			A,:?_HERO_UNDSWIM_DOWN_ANIM
	LDFF_A		SCRIPT_WBANK
	MOVADDR_FF	SCRIPT_WFRAME,?_HERO_UNDSWIM_DOWN_ANIM
	MOVADDR_FF	ACTOR_STATE,?HERO_UNDERSWIM_DOWN
	JP			?HERO_AI_CLOSE

?HERO_UNDERSWIM_DOWN
	CALL		?RANDOM_ENCOUNTER
	CALL		?HERO_AI_OPEN

	; CHECK DIRECTION
	;--------------------------------
	LD			A,(AI_CNT1)
	BIT			BitDown,A
	JP			NZ,_DOWN
	MOVADDR		SCRIPT_WFRAME,?_HERO_UNDTREAD_DOWN_ANIM
	JP			?HERO_AI_FLOAT
	
_DOWN
	BIT			BitLeft,A
	JR			Z,_NO_LEFT
	JP			?HERO_AI_UNDERSWIM_LEFT
	
_NO_LEFT
	BIT			BitRight,A
	JR			Z,_NO_RIGHT
	JP			?HERO_AI_UNDERSWIM_RIGHT

_NO_RIGHT

	; MOVE
	;--------------------------------
	CALL		?HERO_CHECK_CURRENTS
	COLL_DETECT
	CHECK_HOTSPOT
	CALL		?CAMERA_UPDATE

	; CHECK ACTION
	;--------------------------------
	LD			A,(COLL_YMOVE)
	AND			A
	CALL		Z,?HERO_CHECK_ACTION_DOWN

	JP			?HERO_AI_CLOSE

;********************************
;	BC:		Ptr to gob
?HERO_AI_UNDERSWIM_LEFT
	LD			A,%00000001
	LDFF_A		ACTOR_FLAGS
	MOVADDR_FF	SCRIPT_WSTATE,?SCRIPT_START
	LD			A,:?_HERO_UNDSWIM_LEFT_ANIM
	LDFF_A		SCRIPT_WBANK
	MOVADDR_FF	SCRIPT_WFRAME,?_HERO_UNDSWIM_LEFT_ANIM
	MOVADDR_FF	ACTOR_STATE,?HERO_UNDERSWIM_LEFT
	JP			?HERO_AI_CLOSE

?HERO_UNDERSWIM_LEFT
	CALL		?RANDOM_ENCOUNTER
	CALL		?HERO_AI_OPEN

	; CHECK DIRECTION
	;--------------------------------
	LD			A,(AI_CNT1)
	BIT			BitLeft,A
	JR			NZ,_LEFT
	MOVADDR		SCRIPT_WFRAME,?_HERO_UNDTREAD_LEFT_ANIM
	JP			?HERO_AI_FLOAT
	
_LEFT
	LDA_FF		TICKER
	AND			$01
	JP			Z,_NO_DOWN

	LD			A,(AI_CNT1)
	BIT			BitUp,A
	JR			Z,_NO_UP
	LD			A,-$01
	LD			(COLL_YMOVE),A
	JR			_NO_DOWN
	
_NO_UP
	BIT			BitDown,A
	JR			Z,_NO_DOWN
	LD			A,$01
	LD			(COLL_YMOVE),A
	
_NO_DOWN

	; MOVE
	;--------------------------------
	CALL		?HERO_CHECK_CURRENTS
	COLL_DETECT
	CHECK_HOTSPOT
	CALL		?CAMERA_UPDATE

	; CHECK ACTION
	;--------------------------------
	LD			A,(COLL_XMOVE)
	AND			A
	CALL		Z,?HERO_CHECK_ACTION_LEFT

	JP			?HERO_AI_CLOSE

;********************************
;	BC:		Ptr to gob
?HERO_AI_UNDERSWIM_RIGHT
	LD			A,%00000010
	LDFF_A		ACTOR_FLAGS
	MOVADDR_FF	SCRIPT_WSTATE,?SCRIPT_START
	LD			A,:?_HERO_UNDSWIM_RIGHT_ANIM
	LDFF_A		SCRIPT_WBANK
	MOVADDR_FF	SCRIPT_WFRAME,?_HERO_UNDSWIM_RIGHT_ANIM
	MOVADDR_FF	ACTOR_STATE,?HERO_UNDERSWIM_RIGHT
	JP			?HERO_AI_CLOSE

?HERO_UNDERSWIM_RIGHT
	CALL		?RANDOM_ENCOUNTER
	CALL		?HERO_AI_OPEN

	; CHECK DIRECTION
	;--------------------------------
	LD			A,(AI_CNT1)
	BIT			BitRight,A
	JR			NZ,_RIGHT
	MOVADDR		SCRIPT_WFRAME,?_HERO_UNDTREAD_RIGHT_ANIM
	JP			?HERO_AI_FLOAT
	
_RIGHT
	LDA_FF		TICKER
	AND			$01
	JP			Z,_NO_DOWN

	LD			A,(AI_CNT1)
	BIT			BitUp,A
	JR			Z,_NO_UP
	LD			A,-$01
	LD			(COLL_YMOVE),A
	JR			_NO_DOWN
	
_NO_UP
	BIT			BitDown,A
	JR			Z,_NO_DOWN
	LD			A,$01
	LD			(COLL_YMOVE),A
	
_NO_DOWN

	; MOVE
	;--------------------------------
	CALL		?HERO_CHECK_CURRENTS
	COLL_DETECT
	CHECK_HOTSPOT
	CALL		?CAMERA_UPDATE

	; CHECK ACTION
	;--------------------------------
	LD			A,(COLL_XMOVE)
	AND			A
	CALL		Z,?HERO_CHECK_ACTION_RIGHT

	JP			?HERO_AI_CLOSE

;********************************
;	BC:		Ptr to gob
?HERO_AI_UNDERSWIM_UP
	LD			A,%00000000
	LDFF_A		ACTOR_FLAGS
	MOVADDR_FF	SCRIPT_WSTATE,?SCRIPT_START
	LD			A,:?_HERO_UNDSWIM_UP_ANIM
	LDFF_A		SCRIPT_WBANK
	MOVADDR_FF	SCRIPT_WFRAME,?_HERO_UNDSWIM_UP_ANIM
	MOVADDR_FF	ACTOR_STATE,?HERO_UNDERSWIM_UP
	JP			?HERO_AI_CLOSE

?HERO_UNDERSWIM_UP
	CALL		?RANDOM_ENCOUNTER
	CALL		?HERO_AI_OPEN

	; CHECK DIRECTION
	;--------------------------------
	LD			A,(AI_CNT1)
	BIT			BitUp,A
	JR			NZ,_UP
	MOVADDR		SCRIPT_WFRAME,?_HERO_UNDTREAD_UP_ANIM
	JP			?HERO_AI_FLOAT
	
_UP
	BIT			BitLeft,A
	JR			Z,_NO_LEFT
	JP			?HERO_AI_UNDERSWIM_LEFT
	
_NO_LEFT
	BIT			BitRight,A
	JR			Z,_NO_RIGHT
	JP			?HERO_AI_UNDERSWIM_RIGHT

_NO_RIGHT

	; MOVE
	;--------------------------------
	CALL		?HERO_CHECK_CURRENTS
	COLL_DETECT
	CHECK_HOTSPOT
	CALL		?CAMERA_UPDATE

	; CHECK ACTION
	;--------------------------------
	LD			A,(COLL_YMOVE)
	AND			A
	CALL		Z,?HERO_CHECK_ACTION_UP

	JP			?HERO_AI_CLOSE

;********************************
	END
;********************************
