;********************************
; MENU_HISTORIAN.S
;********************************
;	Author:	Erik Hutchinson
;	(c)2000	Interactive Imagination
;	All rights reserved
;********************************
;********************************
;The Main Historian loop that checks for users choices and goes to the appropriate functions
?MENU_HISTORIAN_LOOP
	LD		A,$FF							;precautionary
	LD		(MENU_RETURN_VALUE),A
	MENU_GO
	CALL	?SYSTEM_UPDATE_GAME_NOSCRIPT
	LD		A,(MENU_RETURN_VALUE)
	CP		$FF	
	JR		Z,?MENU_HISTORIAN_LOOP
	
	CP		$FE								;the b button
	JP		Z,?MENU_HISTORIAN_VALID_SELECT
	
	CP		MENU_HISTORIAN_SCROLLBACK		; LEFT: go back in the menu
	JP		Z,?MENU_HISTORIAN_SCROLL_BACK
	CP		MENU_HISTORIAN_SCROLLFORWARD	; RIGHT: go forward in the menu
	JP		Z,?MENU_HISTORIAN_SCROLL_FORWARD

	CP		MENU_HISTORIAN_SPELLS
	JP		Z,?MENU_HISTORIAN_TYPE_CHOICE	;Spells
	CP		MENU_HISTORIAN_ITEMS
	JP		Z,?MENU_HISTORIAN_TYPE_CHOICE	;Items
	CP		MENU_HISTORIAN_RELICS
	JP		Z,?MENU_HISTORIAN_TYPE_CHOICE	;Relics

;not one of the previous choices? then the user must have selected an item
	CALL	?MENU_HISTORIAN_SELECT_ITEM
	JP		?MENU_SPECIAL_END
	
;********************************
;Scrolls back through the inventory list
?MENU_HISTORIAN_SCROLL_BACK
	CALL	?MENU_SPECIAL_PREVIOUS_SCREEN
	SCREEN_HIDE
	CALL	?MENU_SPECIAL_ITEMS_UPDATE
	SCREEN_SHOW

	LD		A,(MENU_SPECIAL_VRAM_ARROW_POS)
	CALL	?MENU_SPECIAL_FORWARD_INSERT
	LD		HL,MENU_SPECIAL_VALID+MENU_HISTORIAN_SCROLLFORWARD	
	LD		A,$01
	LD		(HLD),A
	LD		A,(MENU_SPECIAL_ITEM_NUM)
	SUB		MENU_SPECIAL_NUMBER_ON_SCREEN
	LD		(MENU_SPECIAL_ITEM_NUM),A
	CP		MENU_SPECIAL_NUMBER_ON_SCREEN
	JR		NZ,_CANSCROLL
	LD		A,$00
	LD		(HL),A						;setting up scroll back to be invalid
	CALL	?MENU_SPECIAL_BACK_REMOVAL
	JR		_DONE
	
_CANSCROLL	
	CALL	?MENU_SPECIAL_BACK_INSERTION
	
_DONE
	LD		A,(MENU_RETURN_VALUE)
	LD		(MENU_CURSOR_ID),A
	MENU_INIT	MENU_HISTORIAN_SEL,243,MENU_SPECIAL_VALID,?MENU_SHOP_KEY_START,?MENU_FLASH,(MENU_CURSOR_ID),MENU_CURSOR_BLINK,MENU_CURSOR_SPRITE
	JP		?MENU_HISTORIAN_LOOP
	
;********************************
;Scrolls forward through the inventory list
?MENU_HISTORIAN_SCROLL_FORWARD	
	LD		HL,MENU_SPECIAL_ON_SCREEN
	LD		E,$07
	LD		D,$00
	ADD		HL,DE
	LD		A,(HL)
	INC		A
	LD		(MENU_SPECIAL_TOP_ITEM),A
	CALL	?MENU_SPECIAL_SELL_ON_SCREEN
	SCREEN_HIDE
	CALL	?MENU_SPECIAL_ITEMS_UPDATE
	SCREEN_SHOW
	
	LD		A,(MENU_RETURN_VALUE)
	LD		(MENU_CURSOR_ID),A
	
	CALL	?MENU_SPECIAL_BACK_INSERTION
	LD		A,$01
	LD		HL,MENU_SPECIAL_VALID+MENU_HISTORIAN_SCROLLBACK
	LD		(HLI),A						;setting up scroll back to be valid
	LD		A,(MENU_SPECIAL_TOTAL_ITEMS)
	LD		C,A	
	LD		A,(MENU_SPECIAL_ITEM_NUM)
	ADD		A,MENU_SPECIAL_NUMBER_ON_SCREEN
	LD		(MENU_SPECIAL_ITEM_NUM),A
	CP		C
	JR		C,_CANSCROLL
	
	LD		A,$00						;cant scroll forward anymore
	LD		(HL),A						;HL is already setup at scrollforward point
	LD		A,MENU_HISTORIAN_SCROLLBACK
	LD		(MENU_CURSOR_ID),A	
	CALL	?MENU_SPECIAL_FORWARD_REMOVAL
	JR		_DONE
_CANSCROLL	
	CALL	?MENU_SPECIAL_FORWARD_INSERT

_DONE
	MENU_INIT	MENU_HISTORIAN_SEL,243,MENU_SPECIAL_VALID,?MENU_SHOP_KEY_START,?MENU_FLASH,(MENU_CURSOR_ID),MENU_CURSOR_BLINK,MENU_CURSOR_SPRITE
	JP		?MENU_HISTORIAN_LOOP
	
;********************************
;Initializes the Historian menu, along with the border, and
;displays the initial choices of items, spells, and relics
?MENU_HISTORIAN_START
	CALL		?MENU_SPECIAL_ATTRIB_SETUP
	LD			BC,MENU_HISTORIAN_BG_TABLE
	CALL		?MENU_SPECIAL_READ_DUMP
	LD			DE,$8860
	FSET16		D,E,MENU_VRAM_PTR
	LD		A,104								;8 times the item size..number of blanks
	CALL	?MENU_SPECIAL_BLANK_LINE			;inserts blank spaces in place of items

	LD			BC,MENU_HISTORIAN_STR
	FSET16		B,C,MENU_CUR_CHAR
	LD			A,$10
	LD			(MENU_CHAR_LEFT),A
	CALL_FOREIGN	?MAIN_MENU_PACK_VRAM

	MENU_INIT	MENU_HISTORIAN_SEL,243,MENU_SPECIAL_VALID,?MENU_SHOP_KEY_START,?MENU_FLASH,(MENU_CURSOR_ID),MENU_CURSOR_BLINK,MENU_CURSOR_SPRITE
	SCREEN_SHOW
	CALL		?SYSTEM_UPDATE_GAME_NOSCRIPT
	LD		A,$03
	LD		(MENU_SPECIAL_MENU_SECTION),A
	LD		A,$F9
	LD		(MENU_SPECIAL_VRAM_ARROW_POS),A
	JP		?MENU_HISTORIAN_LOOP

;********************************
;Sets up the users choice and all the variables of either items, spells or relics
?MENU_HISTORIAN_TYPE_CHOICE
	XOR		A
	LD		(MENU_SPECIAL_TOP_ITEM),A
	LD		A,(MENU_RETURN_VALUE)
	LD		(MENU_SPECIAL_MODE),A
	CP		$00
	JR		Z,_ITEMS
	CP		$01
	JR		Z,_SPELLS
	
	LD		A,INV_TYPE_RELIC	
	LD		(MENU_HISTORIAN_TYPE),A
	LD		A,RELIC_NAME_OFFSET
	LD		(MENU_SPECIAL_TYPE_OFFSET),A
	LD		A,RELIC_SIZE
	LD		(MENU_SPECIAL_TYPE_SIZE),A
	LD		HL,RELIC_TABLE
	SET16	H,L,MENU_SPECIAL_TYPE_TABLE
	LD		A,RELIC_NAME_SIZE
	LD		(MENU_SPECIAL_NAME_SIZE),A
	LD		HL,XRAM_INVENTORY_RELICS
	JR		_NEXT
	
_ITEMS
	LD		A,INV_TYPE_ITEM
	LD		(MENU_HISTORIAN_TYPE),A
	LD		A,ITEM_NAME_OFFSET
	LD		(MENU_SPECIAL_TYPE_OFFSET),A
	LD		A,ITEM_SIZE
	LD		(MENU_SPECIAL_TYPE_SIZE),A
	LD		HL,ITEM_TABLE
	SET16	H,L,MENU_SPECIAL_TYPE_TABLE
	LD		A,ITEM_NAME_SIZE
	LD		(MENU_SPECIAL_NAME_SIZE),A
	LD		HL,XRAM_INVENTORY_ITEMS
	JR		_NEXT
	
_SPELLS
	LD		A,INV_TYPE_SPELL
	LD		(MENU_HISTORIAN_TYPE),A
	LD		A,SPELL_NAME_OFFSET
	LD		(MENU_SPECIAL_TYPE_OFFSET),A
	LD		A,SPELL_SIZE
	LD		(MENU_SPECIAL_TYPE_SIZE),A
	LD		HL,SPELL_TABLE
	SET16	H,L,MENU_SPECIAL_TYPE_TABLE
	LD		A,SPELL_NAME_SIZE
	LD		(MENU_SPECIAL_NAME_SIZE),A
	LD		HL,XRAM_INVENTORY_SPELLS
	
_NEXT
	SET16	H,L,MENU_SPECIAL_TYPE_ADD
	LD		C,$F0							;the number of bytes in xram	
	CALL	?MENU_SPECIAL_NUMBER_OF_ITEMS	;calc number of items
	CALL	?MENU_SPECIAL_SELL_ON_SCREEN			;sets up the 5 currently viewable items

	SCREEN_HIDE
	CALL	?MENU_SPECIAL_ITEMS_UPDATE
	SCREEN_SHOW

	CALL	?MENU_SPECIAL_BACK_REMOVAL
	XOR		A
	LD		HL,MENU_SPECIAL_VALID+MENU_HISTORIAN_SCROLLBACK	
	LD		(HLI),A		
	LD		(HL),A
	LD		A,(MENU_SPECIAL_TOTAL_ITEMS)
	LD		C,A
	LD		A,MENU_SPECIAL_NUMBER_ON_SCREEN
	LD		(MENU_SPECIAL_ITEM_NUM),A
	CP		C
	JR		NC,_NOFORWARD

	LD		A,$01
	LD		(HL),A						;setting up scroll forward to be valid
	CALL	?MENU_SPECIAL_FORWARD_INSERT
	JR		_DONE

_NOFORWARD
	CALL	?MENU_SPECIAL_FORWARD_REMOVAL	

_DONE
	LD		A,(MENU_RETURN_VALUE)
	LD		(MENU_CURSOR_ID),A	
	XOR		A
	LD		(MENU_SPECIAL_MENU_SECTION),A
	JP		?MENU_HISTORIAN_VALID_SELECT


	
;********************************	
;Sets up variables that contain the information on the item selected
?MENU_HISTORIAN_SELECT_ITEM	
	LD		A,(MENU_RETURN_VALUE)
	CP		MENU_HISTORIAN_DONE
	JR		Z,_DONE
	SUB		$03								;removing menu position info
	LD		C,A
	LD		B,$00
	LD		HL,MENU_SPECIAL_ON_SCREEN
	ADD		HL,BC
	LD		A,(HL)
	LD		(MENU_HISTORIAN_TYPE_NUMBER),A
	RET
	
_DONE
	LD		A,$FF
	LD		(MENU_HISTORIAN_TYPE_NUMBER),A
	LD		(MENU_HISTORIAN_TYPE),A
	RET

;********************************
?MENU_HISTORIAN_VALID_SELECT
	LD		A,(MENU_SPECIAL_MENU_SECTION)
	CP		$00
	JR		Z,_UPPER
	CP		$03
	JR		NZ,_LOWER
	LD		A,$FF
	LD		(MENU_HISTORIAN_TYPE_NUMBER),A
	LD		(MENU_HISTORIAN_TYPE),A
	JP		?MENU_SPECIAL_END
	
_LOWER
	LD		A,$03
	LD		(MENU_SPECIAL_MENU_SECTION),A
	LD		A,MENU_HISTORIAN_ITEMS
	LD		(MENU_CURSOR_ID),A
	CALL	?MENU_SPECIAL_HAND_REMOVAL
	JR		_DONE

_UPPER
	CALL	?MENU_SPECIAL_UPPER_CHECK
	CP		$00
	JR		Z,_LOWER
	CALL	?MENU_SPECIAL_HAND_INSERTION
	LD		A,MENU_HISTORIAN_ITEM1
	LD		(MENU_CURSOR_ID),A
	LD		A,$01
	LD		(MENU_SPECIAL_MENU_SECTION),A

_DONE
	MENU_INIT	MENU_HISTORIAN_SEL,243,MENU_SPECIAL_VALID,?MENU_SHOP_KEY_START,?MENU_FLASH,(MENU_CURSOR_ID),MENU_CURSOR_BLINK,MENU_CURSOR_SPRITE
	JP		?MENU_HISTORIAN_LOOP

;********************************
	END
;********************************