;********************************
; BATTLE_AI.S
;********************************
;	Author:	Dylan "ExoByte" Mayo
;	(c)2000	Interactive Imagination
;	All rights reserved
;********************************

	LIB		SOURCE\GAME\BATTLE\BATTLEAI\BATTLE_AI_TABLE.S
	LIB		SOURCE\GAME\BATTLE\BATTLEAI\BATTLE_AI_TARGET.S

;********************************
?BTL_AI_LOGIC_RND

	XOR		A
	LD		(BTL_COUNTER),A
	
_FIND_LEARNED_CMD_LOOP
	XOR		A
	LD		HL,BTL_COUNTER
	INC		(HL)
	JR		Z,_LEARNED
	RANDVAL		E
	LD		C,A
	LD		B,6
	CALL	?DIV
	LD		A,L
	AND		A				;2/6 CHANCE FOR JUST FIGHT
	JR		Z,_LEARNED
	DEC		A
	JR		Z,_LEARNED
	
	PUSH	AF
	DEC		A
	LD		HL,BTL_CREATURE
	LD		BC,CREATURE_CMD0LVL
	ADD		HL,BC
	LD		C,A
	LD		B,0
	ADD		HL,BC
	LD		A,(HL)
	CP		$FF
	POP		BC
	LD		A,B
	JR		NZ,_FIND_LEARNED_CMD_LOOP	
		
_LEARNED
	LD		(MENU_RETURN_VALUE),A
	CALL_FOREIGN	?BTL_OPEN_CMD
	
	;Valid targets for this command?
	;--------------------------
	CALL_FOREIGN	?BTL_CHECK_TARGET
	LD		A,(BTL_CMD_VALID)
	AND		A
	JR		Z,_FIND_LEARNED_CMD_LOOP
	
	CALL	?BTL_AI_STD_TARGET_SUITE
	LD		A,(BTL_TARGET_SUITE_SET)
	AND		A
	RET		NZ
	
	;Find some bozo to beat up
	;-------------------------
	LD		A,(BTL_CREATURE_STATUS)
	AND		CONFUSED
	JR		Z,_NOT_CONFUSED
	
	RANDVAL		E
	BIT		4,A
	JR		Z,_ENEMY_TEAM
	JR		_ALLY_TEAM
	
_NOT_CONFUSED
	LD		A,(BTL_CREATURE_RATING)
	CP		BTL_CMD_RATING_HEAL0
	JR		NC,_ENEMY_TEAM	
	
_ALLY_TEAM
	CALL	?BTL_AI_TALLY_R
	RET	
	
_ENEMY_TEAM
	CALL	?BTL_AI_TENEMY_R
	RET	

;********************************
?BTL_AI_DEBUG_CREATURE


;	XOR		A
;	LD		(MENU_RETURN_VALUE),A
;	CALL_FOREIGN	?BTL_OPEN_CMD

;********************************
	XOR		A
	LD		(BTL_COUNTER),A
	
_FIND_LEARNED_CMD_LOOP
	XOR		A
	LD		HL,BTL_COUNTER
	INC		(HL)
	JR		Z,_LEARNED
	RANDVAL		E
	LD		C,A
	LD		B,5
	CALL	?DIV
	LD		A,L
	AND		A
	JR		Z,_LEARNED
	
	PUSH	AF
	DEC		A
	LD		HL,BTL_CREATURE
	LD		BC,CREATURE_CMD0LVL
	ADD		HL,BC
	LD		C,A
	LD		B,0
	ADD		HL,BC
	LD		A,(HL)
	CP		$FF
	POP		BC
	LD		A,B
	JR		NZ,_FIND_LEARNED_CMD_LOOP	
		
_LEARNED
	LD		(MENU_RETURN_VALUE),A
	CALL_FOREIGN	?BTL_OPEN_CMD
	
	;Valid targets for this command?
	;--------------------------
	CALL_FOREIGN	?BTL_CHECK_TARGET
	LD		A,(BTL_CMD_VALID)
	AND		A
	JR		Z,_FIND_LEARNED_CMD_LOOP
	CALL	?BTL_AI_GET_TARGET
	RET
	
;********************************
?BTL_AI_DEBUG_MAGI

	LD		A,(BTL_CREATURE_SLOTS+5)
	AND		A
	JR		Z,_SUMMON
	
	
_NOT_SUMMON
	XOR		A
	LD		(MENU_RETURN_VALUE),A
	CALL_FOREIGN	?BTL_OPEN_CMD
	LD		A,9
	LD		(BTL_CREATURE_TARGET),A
	JR		_DONE
	
_SUMMON
	LD		A,1
	LD		(MENU_RETURN_VALUE),A
	CALL_FOREIGN	?BTL_OPEN_CMD
	LD		A,GRAX
	LD		(BTL_MAGI_SUMMON_CREATURE),A
	LD		A,1
	LD		(BTL_MAGI_SUMMON_LVL),A
	LD		A,20
	LD		(BTL_MAGI_SUMMON_ENERGY),A
	LD		A,5
	LD		(BTL_CREATURE_TARGET),A

_DONE

	RET

;********************************s
?BTL_AI_DEBUG_RND1

	SWITCH_RAM_BANK		WRAM_BATTLE
	
	;DO NOTHING
	XOR		A
	LD		(MENU_RETURN_VALUE),A
	CALL_FOREIGN	?BTL_OPEN_CMD
	LD		A,9
	LD		(BTL_CREATURE_TARGET),A
	JR		_DONE	
	
;_TURN0
;	LD		A,1
;	LD		(MENU_RETURN_VALUE),A
;	CALL_FOREIGN	?BTL_OPEN_CMD
;	FGET16	H,L,BTL_AI_PARAMS
;	LD		BC,BTL_MAGI_SUMMON_CREATURE
;	LD_BCI_HLI
;	LD_BCI_HLI
;	LD_BCI_HLI
;	LD		A,5
;	LD		(BTL_CREATURE_TARGET),A
;	JR		_DONE	
;
;_TURN2
;	LD		A,1
;	LD		(MENU_RETURN_VALUE),A
;	CALL_FOREIGN	?BTL_OPEN_CMD
;	FGET16	H,L,BTL_AI_PARAMS
;	LD		DE,3
;	ADD		HL,DE
;	LD		BC,BTL_MAGI_SUMMON_CREATURE
;	LD_BCI_HLI
;	LD_BCI_HLI
;	LD_BCI_HLI
;	LD		A,8
;	LD		(BTL_CREATURE_TARGET),A

_DONE

	RET
	
;********************************	
?BTL_AI_GET_TARGET
	CALL	?BTL_AI_STD_TARGET_SUITE
	LD		A,(BTL_TARGET_SUITE_SET)
	AND		A
	RET		NZ

	LD		A,(BTL_CREATURE_RATING)
	RES		7,A
	CP		BTL_CMD_RATING_HEAL0
	JR		NC,_HEAL

	RANDVAL	E
	BIT		0,A
	JR		Z,_BOTTOM	
	
	LD		HL,BTL_CREATURE_SLOTS+BTL_ID_ALLY3	
	
_TARGET_LOOP
	LD		A,(HLD)
	AND		A
	JR		Z,_TARGET_LOOP
	
	INC		HL
	
_HAVE_TARGET
	LD		BC,BTL_CREATURE_SLOTS
	TWOS_COMP	B,C
	ADD		HL,BC
	
	LD		A,L
	LD		(BTL_CREATURE_TARGET),A
	
	RET
	
_BOTTOM
	LD		HL,BTL_CREATURE_SLOTS+BTL_ID_ALLY0
	LD		D,4
	
_BOTTOM_LOOP
	LD		A,(HLI)
	AND		A
	JR		NZ,_BOTTOM_DONE
	DEC		D
	JR		NZ,_BOTTOM_LOOP
	
	LD		HL,BTL_CREATURE_SLOTS+BTL_ID_HERO
	JR		_HAVE_TARGET
	
_BOTTOM_DONE
	DEC		HL
	JR		_HAVE_TARGET	
	
_HEAL
	RANDVAL	E
	BIT		0,A
	JR		Z,_HEAL_BOTTOM	
	
	LD		HL,BTL_CREATURE_SLOTS+BTL_ID_ENEMY0	
	
_TARGET_LOOP_HEAL
	LD		A,(HLI)
	AND		A
	JR		Z,_TARGET_LOOP_HEAL
	
	DEC		HL
	JR		_HAVE_TARGET
	
_HEAL_BOTTOM	
	LD		HL,BTL_CREATURE_SLOTS+BTL_ID_ENEMY3
	LD		D,4
	
_BOTTOM_LOOP_HEAL
	LD		A,(HLD)
	AND		A
	JR		NZ,_BOTTOM_DONE_HEAL
	DEC		D
	JR		NZ,_BOTTOM_LOOP_HEAL
	
	LD		HL,BTL_CREATURE_SLOTS+BTL_ID_MAGI
	JR		_HAVE_TARGET
	
_BOTTOM_DONE_HEAL
	INC		HL
	JR		_HAVE_TARGET

;********************************
?BTL_AI_STD_TARGET_SUITE

	LD		A,1
	LD		(BTL_TARGET_SUITE_SET),A

	LD		A,(BTL_CREATURE_TARGET)
	CP		BTL_TARGET_ALLENEMY
	RET		NC	
	
	CP		BTL_TARGET_ME
	JR		NZ,_CHECK_NONE
	FGET16	H,L,BTL_CREATURE_SLOT
	LD		BC,BTL_HERO
	TWOS_COMP	B,C
	ADD		HL,BC
	LD		A,CREATURE_BTL_SIZE
	CALL	?DIV16
	LD		(BTL_CREATURE_TARGET),A
	RET
	
_CHECK_NONE	
	CP		BTL_TARGET_NONE
	RET		Z
	
	CP		BTL_TARGET_ENEMYMAGI_EVASION
	JR		NZ,_DONE
	
	LD		A,(BTL_TARGET_TEAM)
	AND		A
	JR		Z,_TONY_TEAM
	
	XOR		A
	LD		(BTL_CREATURE_TARGET),A
	RET
	
_TONY_TEAM
	LD		A,BTL_ID_MAGI
	LD		(BTL_CREATURE_TARGET),A
	RET
	
_DONE
	XOR		A
	LD		(BTL_TARGET_SUITE_SET),A

	RET

;********************************
	END
;********************************