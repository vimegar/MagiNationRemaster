;********************************
; SCRIPT_XX_TEXT.S
;********************************
;	Author:	Patrick Meehan
;	(c)2000	Interactive Imagination
;	All rights reserved

;********************************
	LIB		SOURCE\ENGINE\SCRIPT\MODULES\TEXTBOX\TEXTBOX_XX_VBLANK.S

;********************************
TEXTBOX_WINDOW_SLIDE	DB		$8F,$8F,$8B,$85,$7F,$7A,$75,$70,$6C,$68

;********************************
?CMD_TEXTCLOSE_XX
	LD			A,(TEXTBOX_OPEN)

	CP			TEXTBOX_STATE_CLOSED
	JP			Z,_RETURN
	CP			TEXTBOX_STATE_BUSY
	RET			Z

	LD			A,TEXTBOX_STATE_BUSY
	LD			(TEXTBOX_OPEN),A

	LD			A,$09
	LD			(SCRIPT_WCOUNT),A

	LD			A,:?VBLANK_TEXTBOX_WINDOW
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR
	MOVADDR		SCRIPT_WSTATE,_LOOP

	;LD			A,(TEXTBOX_SOUND_ENABLE)
	;AND			A
	;RET			Z

	;SOUND_START_SFX		SFXID_WINDOW_SLIDE
	RET

_LOOP
	LD			A,(SCRIPT_WCOUNT)
	AND			A
	JP			Z,_DONE

	LD			C,A
	DEC			A

	LD			B,$00
	LD			(SCRIPT_WCOUNT),A

	LD			HL,TEXTBOX_WINDOW_SLIDE
	ADD			HL,BC
	LD			A,(HL)
	LD			(TEXTBOX_WY),A

	LD			A,:?VBLANK_TEXTBOX_WINDOW
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_WINDOW
	RET

_DONE
	LD			A,$90
	LD			(TEXTBOX_WY),A
	LD			A,:?VBLANK_TEXTBOX_WINDOW
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_WINDOW

	MOVADDR		HFUNC,?HFUNC_IDLE
	MOVADDR		SCRIPT_WSTATE,_REALLY_DONE
	RET

_REALLY_DONE
	LD			A,TEXTBOX_STATE_CLOSED
	LD			(TEXTBOX_OPEN),A

_RETURN
	LD			HL,SCRIPT_WBANK
	RES			$07,(HL)
	MOVADDR		SCRIPT_WSTATE,?SCRIPT_PLAY_NEXT
	RET

;********************************
?CMD_TEXTOPEN_XX

	LD			A,(TEXTBOX_OPEN)

	CP			TEXTBOX_STATE_OPEN
	JP			Z,_BAIL
	CP			TEXTBOX_STATE_BUSY
	RET			Z

	LD			A,TEXTBOX_STATE_BUSY
	LD			(TEXTBOX_OPEN),A

	XOR			A
	LD			(SCRIPT_WCOUNT),A
	
	LD			A,$07
	LD			(TEXTBOX_WX),A

	LD			A,$8F
	LDFF_A		LYC

	LD			A,:?VBLANK_TEXTBOX_CLEAR_ICON
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR_ICON
	MOVADDR		SCRIPT_WSTATE,_TEXT_CLEAR
	MOVADDR		HFUNC,?HFUNC_WINDOW

	;LD			A,(TEXTBOX_SOUND_ENABLE)
	;AND			A
	;RET			Z

	;SOUND_START_SFX		SFXID_WINDOW_SLIDE
	RET

_TEXT_CLEAR
	LD			A,:?VBLANK_TEXTBOX_CLEAR
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR
	MOVADDR		SCRIPT_WSTATE,_TEXT_CLEAR2
	RET

_TEXT_CLEAR2
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR2
	MOVADDR		SCRIPT_WSTATE,_TEXT_CLEAR3
	RET

_TEXT_CLEAR3
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR3
	MOVADDR		SCRIPT_WSTATE,_LOOP
	RET

_LOOP
	LD			A,(SCRIPT_WCOUNT)
	CP			$0A
	JP			Z,_DONE

	LD			E,A
	INC			A

	LD			D,$00
	LD			(SCRIPT_WCOUNT),A

	LD			HL,TEXTBOX_WINDOW_SLIDE
	ADD			HL,DE
	LD			A,(HL)
	LD			(TEXTBOX_WY),A

	LD			A,:?VBLANK_TEXTBOX_WINDOW
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_WINDOW
	RET

_DONE
	LD			HL,SCRIPT_WBANK
	RES			$07,(HL)
	MOVADDR		SCRIPT_WSTATE,?SCRIPT_TEXT_INIT_OPEN
	RET

_BAIL
	LD			HL,SCRIPT_WBANK
	RES			$07,(HL)
	MOVADDR		SCRIPT_WSTATE,?SCRIPT_PLAY_NEXT
	RET

;********************************
	END
;********************************