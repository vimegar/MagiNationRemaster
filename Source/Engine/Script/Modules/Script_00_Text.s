;********************************
; SCRIPT_00_TEXT.S
;********************************
;	Author:	Patrick Meehan
;	(c)2000	Interactive Imagination
;	All rights reserved

;********************************
	LIB		SOURCE\ENGINE\SCRIPT\MODULES\TEXTBOX\TEXTBOX_INIT.S
	LIB		SOURCE\ENGINE\SCRIPT\MODULES\TEXTBOX\TEXTBOX_TABLE.S

;********************************
?CMD_TEXTBYTE
	BATTERY_SET_BANK	RAMB_GAMESTATE
	BATTERY_ON
	SCRIPT_WORD			H,L
	PUSH				BC
	CALL				?TEXT_VAL_TO_FORMAT8
	POP					BC
	BATTERY_OFF
	JP					?SCRIPT_PLAY_NEXT

;********************************
?CMD_TEXTCLEAR

	LD			A,(TEXTBOX_OPEN)
	CP			TEXTBOX_STATE_OPEN
	JP			NZ,?SCRIPT_PLAY_NEXT

	LD			A,:?VBLANK_TEXTBOX_CLEAR
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR
	MOVADDR		SCRIPT_WSTATE,?SCRIPT_TEXT_INIT
	RET

;********************************
?CMD_TEXTCLOSE

	LD			HL,SCRIPT_WBANK
	SET			$07,(HL)
	MOVADDR		SCRIPT_WSTATE,?CMD_TEXTCLOSE_XX
	RET

;********************************
?CMD_TEXTICON
	
	CALL			?SCRIPT_TEXT_FORCE_OPEN

	LD			A,:?VBLANK_TEXTBOX_CLEAR
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CLEAR
	MOVADDR		SCRIPT_WSTATE,_ICON
	RET

_ICON
	LD			H,B
	LD			L,C
	
	LD			A,(HLI)
	LD			E,A
	AND			$F0
	LD			(VBLANK_SOURCE),A
	LD			A,(HLI)
	LD			(VBLANK_SOURCE+1),A
	SET16		H,L,SCRIPT_WFRAME
	
	LD			A,E
	AND			$0F
	LD			(VBLANK_BANK),A
	
	LD			HL,TEXTBOX_ICON_CHR
	LD			A,(TEXTBOX_ICON_TOGG)
	ADD			A,A
	LD			C,A
	XOR			A
	LD			B,A
	ADD			HL,BC
	LD			A,(HLI)
	LD			H,(HL)
	LD			L,A
	SET16		H,L,VBLANK_DEST
	
	LD			A,$1A
	LD			(SCRIPT_WCOUNT),A
	
	LD			A,$01
	LD			(VBLANK_VBK),A

	MOVADDR		SCRIPT_WSTATE,_LOOP
	RET
	
_LOOP
	LD			A,(SCRIPT_WCOUNT)
	DEC			A
	JR			Z,_DONE
	LD			(SCRIPT_WCOUNT),A
	
	MOVADDR		VBLANK_FUNC,?VBLANK_COPY_TILE
	RET

_DONE
	LD			A,:?VBLANK_TEXTBOX_ICON
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_ICON
	MOVADDR		SCRIPT_WSTATE,?SCRIPT_TEXT_INIT
	RET

;********************************
?CMD_TEXTMENU

	CALL			?SCRIPT_TEXT_FORCE_OPEN

	SCRIPT_BYTE		E
	FSET16			B,C,SCRIPT_WFRAME

	LD		A,1
	LD		HL,MENU_SPEC_CHOICE_ARRAY
	LD		(HLI),A
	XOR		A
	LD		(HLI),A
	LD		(HLI),A
	LD		(HLI),A	
	
	LD		A,E
	DEC		A
	LD		HL,MENU_SPEC_CHOICE_ARRAY+1
		
_LOOP
	LD		(HLI),A
	DEC		A
	JR		NZ,_LOOP	
	
	MENU_INIT		SPECIALITY_4_CHOICE,241,MENU_SPEC_CHOICE_ARRAY,?MENU_FLASH,?MENU_FLASH,0,MENU_CURSOR_BLINK,MENU_CURSOR_BG

	MOVADDR_FF		SCRIPT_WSTATE,_MENU_LUPE
	RET

_MENU_LUPE
	PUSH	BC
	MENU_GO
	LD		A,(MENU_RETURN_VALUE)
	LD		L,A
	CP		$FF
	POP		BC
	RET		Z
	
	LD		A,L
	ADD		A,L
	ADD		A,L
	
	LD		L,A
	LD		H,$00
	ADD		HL,BC
	
	SET16		H,L,SCRIPT_WFRAME
	MOVADDR		SCRIPT_WSTATE,?CMD_JUMP
	RET

;********************************
?CMD_TEXTOPEN

	LD			HL,SCRIPT_WBANK
	SET			$07,(HL)
	MOVADDR		SCRIPT_WSTATE,?CMD_TEXTOPEN_XX
	RET

;********************************
?CMD_TEXTWORD
	BATTERY_SET_BANK	RAMB_GAMESTATE
	BATTERY_ON
	SCRIPT_WORD			H,L
	PUSH				BC
	CALL				?TEXT_VAL_TO_FORMAT16
	POP					BC
	BATTERY_OFF
	JP					?SCRIPT_PLAY_NEXT

;********************************
?CMD_TEXTWRITE
	
	CALL			?SCRIPT_TEXT_FORCE_OPEN

	SET16			B,C,TEXT_STRING
	TEXT_SET_MODE	?TEXT_CHAR
	MOVADDR			SCRIPT_WSTATE,_LOOP
	RET

_LOOP
	TEXT_UPDATE
	LD			A,D
	AND			A
	RET			Z

	MOVADDR		TEXT_BG_DEST,TEXTBOX_LINE_3
	LD			A,(TEXT_STRING)
	LDFF_A		SCRIPT_WFRAME
	LD			A,(TEXT_STRING+$01)
	LDFF_A		SCRIPT_WFRAME+$01

	LD			A,D

	CP			?EOF
	JR			NZ,_NO_EOF
	MOVADDR		SCRIPT_WSTATE,?SCRIPT_PLAY_NEXT
	RET

_NO_EOF
	CP			?HURRY
	JR			NZ,_WAIT_PRESS
	MOVADDR		SCRIPT_WSTATE,?CMD_TEXTWRITE
	JR			_NEXT_TEXT_WRITE

_WAIT_PRESS
	MOVADDR		SCRIPT_WSTATE,_WAIT_PRESS
	LD			A,(CNT1)
	AND			$01<<BitA
	JR			NZ,_NEXT_TEXT_WRITE
	;LD			A,:?VBLANK_TEXTBOX_CURSOR
	;LD			(VBLANK_BANK),A
	;MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_CURSOR
	RET

_NEXT_TEXT_WRITE	
	LD			A,(TEXTBOX_AUTOSCROLL)
	AND			A
	JR			Z,_SCROLL
	DEC			A
	LD			(TEXTBOX_AUTOSCROLL),A
	MOVADDR		SCRIPT_WSTATE,?CMD_TEXTWRITE
	RET

_SCROLL
	MOVADDR		SCRIPT_WSTATE,_SCROLL2
	LD			A,:?VBLANK_TEXTBOX_SCROLL
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_SCROLL
	RET

_SCROLL2
	MOVADDR		SCRIPT_WSTATE,_SCROLL_DELAY
	LD			A,:?VBLANK_TEXTBOX_SCROLL2
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_SCROLL2
	RET

_SCROLL_DELAY
	MOVADDR		SCRIPT_WSTATE,_SCROLL_DELAY2
	RET

_SCROLL_DELAY2
	MOVADDR		SCRIPT_WSTATE,_SCROLL3
	RET

_SCROLL3
	MOVADDR		SCRIPT_WSTATE,?CMD_TEXTWRITE
	LD			A,:?VBLANK_TEXTBOX_SCROLL3
	LD			(VBLANK_BANK),A
	MOVADDR		VBLANK_FUNC,?VBLANK_TEXTBOX_SCROLL3
	RET

;********************************
?SCRIPT_TEXT_FORCE_OPEN

	LD			A,(TEXTBOX_OPEN)
	CP			TEXTBOX_STATE_OPEN
	JR			Z,_START

	DEC			BC
	SET16_FF	B,C,SCRIPT_WFRAME
	MOVADDR		SCRIPT_WSTATE,?CMD_TEXTOPEN

	POP			AF
	RET

_START
	RET

;********************************
?SCRIPT_TEXT_INIT_OPEN
	LD			A,TEXTBOX_STATE_OPEN
	LD			(TEXTBOX_OPEN),A

?SCRIPT_TEXT_INIT

	;SET TO HAVE CURSOR ON AT FIRST
	;LD			A,$94				;UPPERBIT SET + 20
	;LD			(TEXT_CURSOR_BLINK),A
	
	LD			A,$01
	LD			(TEXTBOX_AUTOSCROLL),A
	TEXT_SETUP	TEXTBOX_VRAM_TEXT,$01,TEXTBOX_LINE_1,$1E
	
	JP			?SCRIPT_PLAY_NEXT

;********************************
	END
;********************************