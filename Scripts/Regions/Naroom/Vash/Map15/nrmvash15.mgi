/********************************/
%INCLUDE			INCLUDES\INCLUDES.MGI
%INCLUDE			REGIONS\NAROOM\VASH\MAP15\NRMVASH15_DR.MGI
%INCLUDE			REGIONS\NAROOM\VASH\MAP15\NRMVASH15_SICKO.MGI
%INCLUDE			REGIONS\NAROOM\VASH\MAP15\NRMVASH15_RETURN.MGI
%INCLUDE			REGIONS\NAROOM\VASH\MAP15\NRMVASH15_TXT.MGI
%INCLUDE			REGIONS\NAROOM\VASH\MAP15\NRMVASH15_PARTY.MGI

/********************************/
?_NRMVASH15
	If						([GC != GC_ARDERIAL_GEYSER],_SKIP)
	SetXRAMByte				(GiaEidon_State,1)
_SKIP
	Switch					([GAMECOUNT])
							(GC_OPENING,?_NRMVASH15_OPENING)
							(GC_NRM_INTRO,?_NRMVASH15_NRM_INTRO)
							(GC_FIND_ORWIN,?_GENERIC_NRMVASH15)
							(GC_END_FUNERAL,?_NRMVASH15_FUNERAL)
							(GC_END_PARTY,?_NRMVASH15_PARTY)

	If						([GC  >= GC_BEAT_OROTHE],_BEAT_OROTHE)
	
	If						([Sick_Orwin],?_NRMVASH15_SICK_PRE_OROTHE)
	JumpLocal				(?_NRMVASH15_WELL_PRE_OROTHE)

_BEAT_OROTHE
	If						([Sick_Orwin],?_NRMVASH15_SICK_POST_OROTHE)
	JumpLocal				(?_NRMVASH15_WELL_POST_OROTHE)

/********************************//********************************/
// GENERIC
/********************************//********************************/
?_GENERIC_NRMVASH15
	SceneNew				()
	LoadMap					(MAP_NRMVASH15)
	LoadHotspots			(HS_NRMVASH15)
	LoadPalette				(PAL_HERO)
	LoadTriggers			(TR_NRMVASH15)
	ThatActorInit			(HEROACTOR,?HERO_AI,14,9,20,0,?_HERO_STAND_UP_ANIM,?_END)
	ThatActorStart			(HEROACTOR)
	HeroFromDoor			()
	HeroSetCamera			()
	SongStart				(SONGID_orwin)
	SceneReady				()
	End						()
/********************************//********************************/
// END GENERIC
/********************************//********************************/


/********************************//********************************/
// VASH NAROOM INTRO & EXPLORATION
/********************************//********************************/
?_NRMVASH15_NRM_INTRO
	If                      ([Nrm_Intro_Count >= 4],?_GENERIC_NRMVASH15)
	SceneNew				()
	LoadMap					(MAP_NRMVASH15)
	LoadHotspots			(HS_NRMVASH15)
	LoadPalette				(PAL_HERO)
	LoadTriggers			(TR_NRMVASH15)
	ThatActorInit			(HEROACTOR,?HERO_AI,14,9,20,0,?_HERO_STANDL_ANIM,?_END)
	ThatActorInit			(ACTOR00,?TALKER_AI,7,5,20,0,?_EIDON_STAND_DOWN_ANIM,_EIDON_TALKER)
	LoadVRAMTiles			(48,BIT_EIDON,0,0)
	ThatActorStart			(HEROACTOR)
	ThatActorStart			(ACTOR00)
	HeroFromDoor			()
	HeroSetCamera			()
	SongStart				(SONGID_orwin)
	SceneReady				()
	End						()

/********************************/
// ACTORS
_EIDON_TALKER
	SetText					(_TXT_YOU_SHOULD_GO)
	Jump					(?_FACE_EIDON)

/********************************/
// TEXT
_TXT_YOU_SHOULD_GO
	If						([Nrm_Intro_Count],_TXT_AFTER_PRUITT)
	TextOpen				()
	TextIcon				(ICON_EIDON)
	TextWrite				("You should go=")
							("visit Pruitt.")
							("Her shop is in=")
							("the Southeast")
							("of Vash Naroom.")
	TextClose				()
	ThisActorRestoreState	()
	
_TXT_AFTER_PRUITT
	TextOpen				()
	TextIcon				(ICON_EIDON)
	TextWrite				("Hey Tony! you=")
							("should check")
							("out the rest of=")
							("Vash Naroom.")
							("Make sure you=")
							("talk with Poad")
							("and Evu.")
	TextClose				()
	ThisActorRestoreState	()
/********************************//********************************/
// END VASH NAROOM INTRO & EXPLORATION
/********************************//********************************/


/********************************//********************************/
// EIDON & ORWIN ARGUE
/********************************//********************************/
?_NRMVASH15_OPENING
	SceneNew				()
	LoadMap					(MAP_NRMVASH15)
	LoadHotspots			(HS_NRMVASH15)
	LoadPalette				(PAL_HERO)
	LoadTriggers			(TR_NRMVASH15)
	ThatActorInit			(HEROACTOR,?ANIM_AI,13,6,20,0,_TONY,?_END)
	ThatActorInit			(ACTOR00,?TALKER_AI,5,6,20,0,_EIDON,_EIDON_TALKER)
	ThatActorInit			(ACTOR01,?ANIM_AI,5,5,20,48,_ORWIN,?_END)
	ThatActorStart			(ACTOR00)
	ThatActorStart			(ACTOR01)
	LoadVRAMTiles			(48,BIT_EIDON,0,0)
	LoadVRAMTiles			(39,BIT_ORWIN,48,0)
	SetCamera				(5,6)
	SongStart				(SONGID_orwin)
	SceneReady				()
	SetGamecount			(GC_NRM_INTRO)
	End						()

/********************************/
// ACTORS
_TONY
	ThisActorSetAnimDelay	(?_HERO_WALK_UP_ANIM,4)
							(?_HERO_WALK_LEFT_ANIM,104)
							(?_HERO_STAND_LEFT_ANIM,0)

	SetText					(_TXT_THIS_MUST_BE_THE_BOY)
	WaitEventText			(1)
	ThisActorSetAnimDelay	(?_HERO_STAND_UL_ANIM,16)
	WaitEventText			(5)
	Delay					(8)
	ThisActorSetAnim		(?_HERO_STAND_UP_ANIM)
	WaitEventText			(42)
	ThisActorSetAnim		(?_HERO_STAND_UL_ANIM)
	Delay					(48)
	ThisActorSetAnim		(?_HERO_STAND_LEFT_ANIM)
	Delay					(80)
	ThisActorSetAnim		(?_HERO_STAND_UL_ANIM)
	End						()

_EIDON
	ThisActorSetAnim		(?_EIDON_STAND_UP_ANIM)

	SetText					(_TXT_NO_I_WILL_NOT)
	WaitAnyEventText		()
	Delay					(22)
	ThisActorSetAnimDelay	(?_EIDON_STAND_UR_ANIM,24)
							(?_EIDON_STAND_UP_ANIM,40)
							(?_EIDON_STAND_UL_ANIM,24)
	ThisActorSetAnim		(?_EIDON_STAND_UP_ANIM)
	WaitEventText			(1)
	ThisActorSetAnimDelay	(?_EIDON_WALK_DOWN_ANIM,4)
							(?_EIDON_WALK_LEFT_ANIM,24)
							(?_EIDON_STAND_LEFT_ANIM,16)
							(?_EIDON_WALK_RIGHT_ANIM,48)
							(?_EIDON_STAND_RIGHT_ANIM,16)
							(?_EIDON_WALK_LEFT_ANIM,48)
							(?_EIDON_STAND_LEFT_ANIM,16)
							(?_EIDON_WALK_RIGHT_ANIM,48)
							(?_EIDON_STAND_RIGHT_ANIM,16)
							(?_EIDON_WALK_LEFT_ANIM,48)
							(?_EIDON_STAND_LEFT_ANIM,16)
							(?_EIDON_WALK_RIGHT_ANIM,24)
	ThisActorSetAnim		(?_EIDON_STAND_UP_ANIM)
	WaitEventMaster			(82)
	Delay					(22)
	ThisActorSetAnimDelay	(?_EIDON_STAND_UR_ANIM,24)
							(?_EIDON_STAND_UP_ANIM,40)
							(?_EIDON_STAND_UL_ANIM,24)
	ThisActorSetAnim		(?_EIDON_STAND_UP_ANIM)
	WaitEventText			(4)
	ThisActorSetAnim		(?_EIDON_STAND_UR_ANIM)
	WaitEventText			(1)
	ThisActorSetAnimDelay	(?_EIDON_STAND_UP_ANIM,32)
							(?_EIDON_STAND_UR_ANIM,0)
	WaitEventText			(5)
	ThisActorSetAnimDelay	(?_EIDON_WALK_UP_ANIM,16)
							(?_EIDON_STAND_RIGHT_ANIM,16)
							(?_EIDON_STAND_RIGHT_ANIM,0)
	WaitEventText			(1)
	ThisActorSetAnim		(?_EIDON_STAND_DR_ANIM)
	WaitEventText			(11)
	ThisActorSetAnim		(?_EIDON_STAND_RIGHT_ANIM)
	WaitEventText			(42)
	Delay					(32)
	ThisActorSetAnim		(?_EIDON_STAND_DR_ANIM)
	Delay					(96)
	ThisActorSetAnim		(?_EIDON_STAND_DR_ANIM)

	SetText					(_TXT_SO_WHAT_NOW)
	WaitEventText			(50)
	ThatActorSetAI			(HEROACTOR,?HERO_AI)
	ThatActorSetScript		(HEROACTOR,?_HERO_STAND_UP_ANIM)
	ThisActorSetAnimDelay	(?_EIDON_WALK_RIGHT_ANIM,32)
							(?_EIDON_WALK_UP_ANIM,4)
							(?_EIDON_STAND_UP_ANIM,48)
							(?_EIDON_STAND_LEFT_ANIM,8)
							(?_EIDON_STAND_DOWN_ANIM,0)
	End						()

_ORWIN
	ThisActorSetAnim		(?_ORWIN_STAND_DOWN_ANIM)
	WaitAnyEventText		()
	ThisActorSetAnimDelay	(?_ORWIN_WALK_RIGHT_ANIM,24)
							(?_ORWIN_STAND_RIGHT_ANIM,16)
							(?_ORWIN_WALK_LEFT_ANIM,48)
							(?_ORWIN_STAND_LEFT_ANIM,16)
							(?_ORWIN_WALK_RIGHT_ANIM,24)
	ThisActorSetAnim		(?_ORWIN_STAND_DOWN_ANIM)
	WaitAnyEventText		()
	ThisActorSetAnimDelay	(?_ORWIN_WALK_UP_ANIM,8)
							(?_ORWIN_STAND_UP_ANIM,0)
	WaitAnyEventText		()
	ThisActorSetAnimDelay	(?_ORWIN_STAND_UL_ANIM,6)
							(?_ORWIN_STAND_UR_ANIM,6)
							(?_ORWIN_STAND_UL_ANIM,6)
							(?_ORWIN_STAND_UR_ANIM,6)
							(?_ORWIN_STAND_UP_ANIM,32)
							(?_ORWIN_STAND_DOWN_ANIM,32)
							(?_ORWIN_WALK_DOWN_ANIM,8)
							(?_ORWIN_STAND_DOWN_ANIM,0)
	WaitAnyEventText		()
	ThisActorSetAnimDelay	(?_ORWIN_RUN_UP_ANIM,8)
							(?_ORWIN_RUN_DOWN_ANIM,8)	
							(?_ORWIN_RUN_LEFT_ANIM,8)	
							(?_ORWIN_RUN_RIGHT_ANIM,8)	
							(?_ORWIN_STAND_DOWN_ANIM,8)
							(?_ORWIN_RUN_RIGHT_ANIM,8)
							(?_ORWIN_RUN_UP_ANIM,8)	
							(?_ORWIN_RUN_LEFT_ANIM,8)	
							(?_ORWIN_RUN_DOWN_ANIM,8)	
							(?_ORWIN_STAND_DOWN_ANIM,32)	
							(?_ORWIN_STAND_DL_ANIM,8)	
							(?_ORWIN_STAND_DR_ANIM,8)	
							(?_ORWIN_STAND_DL_ANIM,8)	
							(?_ORWIN_STAND_DOWN_ANIM,0)
	WaitAnyEventText		()
	SetEventMaster			(82)	
	ThatActorStart			(HEROACTOR)
	ThisActorSetAnimDelay	(?_ORWIN_WALK_RIGHT_ANIM,24)
							(?_ORWIN_STAND_RIGHT_ANIM,16)
							(?_ORWIN_WALK_LEFT_ANIM,48)
							(?_ORWIN_STAND_LEFT_ANIM,16)
							(?_ORWIN_WALK_RIGHT_ANIM,24)
	ThisActorSetAnim		(?_ORWIN_STAND_DR_ANIM)
	WaitEventText			(5)
	ThisActorSetAnimDelay	(?_ORWIN_WALK_RIGHT_ANIM,24)	
							(?_ORWIN_STAND_DOWN_ANIM,0)
	WaitEventText			(12)
	ThisActorSetAnimDelay	(?_ORWIN_WALK_UP_ANIM,8)
							(?_ORWIN_STAND_UP_ANIM,32)
							(?_ORWIN_STAND_DOWN_ANIM,0)

	SetText					(_TXT_IVE_BEEN_FROM_HERE)
	WaitAnyEventText		()
	Delay					(32)
	ThisActorSetAnimDelay	(?_ORWIN_WALK_DOWN_ANIM,8)
							(?_ORWIN_STAND_DOWN_ANIM,16)

	SetText					(_TXT_THIS_IS_A_DILEMMA)
	WaitEventText			(1)
	ThisActorSetAnim		(?_ORWIN_STAND_LEFT_ANIM)
	WaitEventText			(42)
	ThisActorSetAnimDelay	(?_ORWIN_WALK_LEFT_ANIM,16)
							(?_ORWIN_WALK_DOWN_ANIM,24)
							(?_ORWIN_WALK_RIGHT_ANIM,56)
							(?_ORWIN_WALK_UP_ANIM,120)
	ThisActorStop			()
	End						()

_EIDON_TALKER
	SetText					(_TXT_GO_SEE_PRUITT)
	Jump					(?_FACE_EIDON)

/********************************/
// TEXT
_TXT_GO_SEE_PRUITT
	TextOpen				()
	TextIcon				(ICON_EIDON)
	TextWrite				("You should go=")
							("visit Pruitt.")
							("Her shop is in=")
							("the Southeast")
							("of Vash Naroom.")
	TextClose				()
	ThisActorRestoreState	()

_TXT_NO_I_WILL_NOT
	TextOpen				()
	TextIcon				(ICON_ORWIN)
	TextWrite				("No! I will not=")
							("go through this ")
							("again!")
	SetAnyEventText			()
	TextIcon				(ICON_EIDON)
	TextWrite				("But Orwin...")
	TextIcon				(ICON_ORWIN)
	TextWrite				("NO, Eidon! You=")
							("think someone")
							("is Kyros every=")
							("year! Let's")
							("see, who was it=")
							("last year?")
							("Poad? What were=")
							("you thinking?!")
	SetEventText			(1)
	TextIcon				(ICON_EIDON)
	TextWrite				("I could have=")
							("sworn the food")
							("he cooked gave=")
							("me superhuman")
							("strength! But=")
							("this time-")
	SetAnyEventText			()
	TextIcon				(ICON_ORWIN)
	TextWrite				("...and before=")
							("that was Yaki!")
							("We couldn't")
							("shut him up for=")
							("months! He went")
							("around shouting=")
	SetAnyEventText			()
	TextWrite				("OOOH YEAH! I'M")
							("THE GREAT MAGUS=")
							("KYROS! DIG IT!")
							("WOO!")
	TextIcon				(ICON_EIDON)
	TextWrite				("Okay, okay, I'm=")
							("SORRY!! If")
							("you'd just=")
							("listen, this")
							("time...")
	TextClose				()
	SetEventText			(1)
	End						()

_TXT_THIS_MUST_BE_THE_BOY
	TextOpen				()
	TextIcon				(ICON_EIDON)
	TextWrite				(".........")
	TextIcon				(ICON_ORWIN)
	TextWrite				("Ah, this must=")
	SetEventText			(4)
	TextWrite				("be the boy.")
	TextIcon				(ICON_EIDON)
	TextWrite				("Yes. Tony, I'd=")
							("like you to")
							("meet Orwin,=")
							("Elder of Vash")
							("Naroom.")
	SetEventText			(1)	
	Delay					(32)	
	TextIcon				(ICON_TONY)
	TextWrite				("Nice to meet=")
							("you.")
	SetEventText			(5)
	TextIcon				(ICON_ORWIN)
	TextWrite				("Yes, yes. So=")
							("you wish to get")
							("home, right?")
							("Where did you")
							("say you were")
							("from?")
	SetEventText			(1)
	TextIcon				(ICON_TONY)
	TextWrite				("Tavel Gorge.")
	SetEventText			(11)
	TextIcon				(ICON_ORWIN)
	TextWrite				("Tavel...")
	TextWrite				("Gorge...?=")
							("Hmmm...")
	TextClose				()
	SetEventText			(12)
	End						()

_TXT_IVE_BEEN_FROM_HERE
	TextOpen				()
	TextIcon				(ICON_ORWIN)
	TextWrite				("I've been from=")
							("here to Orothe")
							("and back, but=")
							("that name")
							("doesn't ring a=")
							("bell.")
	TextClose				()
	SetEventText			(1)
	End						()

_TXT_THIS_IS_A_DILEMMA
	TextOpen				()
	TextIcon				(ICON_ORWIN)
	TextWrite				("Well, this is a=")
							("dilemma. I will")
							("try to figure=")
							("out a way to")
							("get you home,=")
							("but you must")
							("stay in town=")
							("until then.")
	TextIcon				(ICON_EIDON)
	TextWrite				("I told you, I=")
							("don't think")
							("he's from this=")
							("world.")
	SetEventText			(1)
	TextIcon				(ICON_ORWIN)
	TextWrite				("Quiet you! I=")
							("need to think.")
	TextClose				()
	SetEventText			(42)
	End						()

_TXT_SO_WHAT_NOW
	TextOpen				()
	TextIcon				(ICON_TONY)
	TextWrite				("So... what now?")
	TextIcon				(ICON_EIDON)
	TextWrite				("Well, I guess=")
							("you'll have to")
							("stay in town.=")
							("We'll get you")
							("out of here=")
							("soon enough. In")
							("the mean time,=")
							("take your")
							("crystal to=")
							("Pruitt, the")
							("Ringsmith. If=")
							("you get hungry,")
							("go see Poad at=")
							("the Inn.")
	TextClose				()
	SetEventText			(50)
	End						()
/********************************//********************************/
// END EIDON & ORWIN ARGUE
/********************************//********************************/
