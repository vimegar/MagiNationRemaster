/********************************//********************************/
// BEAT NAROOM
/********************************//********************************/
?_WVEGIASHUT02_BEAT_NAROOM
	If						([GiaScene_BeatNaroom],?_WVEGIASHUT02_GENERIC)
	SetXRAMBit				(GiaScene_BeatNaroom,TRUE)
	SceneNew				()
	LoadMap					(MAP_WVEGIASHUT02)
	LoadHotspots			(HS_WVEGIASHUT02)
	LoadTriggers			(TR_WVEGIASHUT02)
	LoadPalette				(PAL_HERO)
	ThatActorInit			(HEROACTOR,?ANIM_AI,7,11,15,0,_TONY,?_DONT_TALK)
	ThatActorInit			(ACTOR00,?TALKER_AI,8,4,15,0,_GIA,?_GAMECOUNT_GIA_TALKER)
	ThatActorInit			(ACTOR01,?ANIM_AI,8,6,15,34,?_GLYPH_NUDE_ANIM,?_DONT_TALK)
	LoadVRAMTiles			(34,BIT_GIA,0,0)
	LoadVRAMTiles			(4,BIT_NUDEGLYPH,34,0)
	ThatActorStart			(HEROACTOR)
	ThatActorStart			(ACTOR00)
	HeroSetCamera			()
	SongStart				(SONGID_giahut)
	SceneReady				()
	End						()

_TONY
	ThisActorSetAnimScroll	(?_HERO_WALK_UP_ANIM,32,0,-1)
	ThisActorSetAnimDelay	(?_HERO_WALK_RIGHT_ANIM,32)
							(?_HERO_WALK_UP_ANIM,64)
							(?_HERO_STAND_LEFT_ANIM,0)
	SetEventMaster			(1)
	SetText					(_TXT_LOOK_WHAT_I_FOUND)
	WaitEventText			(10)						// Place glyph on table
	ThisActorSetAnimDelay	(?_HERO_STAND_LEFT_ANIM,32)
	ThatActorStart			(ACTOR01)
	Delay					(24)
	ThisActorSetAnim		(?_HERO_STAND_UL_ANIM)
	WaitEventText			(11)						// Tony face up-left
	ThatActorStop			(ACTOR01)
	ThisActorSetAnimDelay	(?_HERO_STAND_LEFT_ANIM,64)
							(?_HERO_STAND_UL_ANIM,0)
	End						()
	
_GIA
	ThisActorSetAnim		(?_GIA_STAND_DOWN_ANIM)
	WaitEventMaster			(1)
	ThisActorSetAnim		(?_GIA_STAND_DR_ANIM)
	WaitEventText			(10)						// Place glyph on table
	Delay					(24)
	ThisActorSetAnim		(?_GIA_STAND_DOWN_ANIM)
	WaitEventText			(12)						// Gia look at Tony
	ThisActorSetAnim		(?_GIA_STAND_DR_ANIM)
	WaitEventText			(13)						// Gia psychic powerz
	ThisActorSetAnimDelay	(?_GIA_WALK_UP_ANIM,8)
							(?_GIA_STAND_UP_ANIM,80)
							(?_GIA_WALK_DOWN_ANIM,8)
							(?_GIA_STAND_DR_ANIM,0)
	WaitEventText			(14)						// Look at blades (face Tony)
	ThisActorSetAnimDelay	(?_GIA_WALK_DOWN_ANIM,8)
							(?_GIA_STAND_RIGHT_ANIM,32)
							(?_GIA_WALK_UP_ANIM,8)
							(?_GIA_STAND_DR_ANIM,0)
	End						()

/********************************/
// TEXT
_TXT_LOOK_WHAT_I_FOUND
	SetXRAMByte			(GS_Glyph_Count,GLYPH_NRM_STONE)

	TextOpen			()
	TextIcon			(ICON_TONY)
	TextWrite			("Gia, look what=")
						("I found at the")
						("Geyser.")
	Delay				(32)
	TextIcon			(ICON_GIA)
	TextWrite			("A stone? What=")
	         			("an unusual")
	         			("shape... this=")
	         			("is what formed")
	         			("the Geyser?")
	TextIcon			(ICON_TONY)
	TextWrite			("I guess. The=")
	         			("geyser closed")
	         			("when I removed=")
	         			("this from it.")
	TextIcon			(ICON_GIA)
	TextWrite			("Hmm... the=")
	         			("shape is very")
	         			("familiar.=")
	         			("Kyros, could")
	         			("you show me=")
	         			("the Core Glyph")
	         			("again?")
	SetEventText		(10)				// Place glyph on table
	Delay				(112)
	TextIcon			(ICON_GIA)
	TextWrite			("Aha! That slot=")
	         			("is the exact")
	         			("same shape and=")
	         			("size as this")
	         			("stone! Kyros,=")
	         			("put the stone")
	         			("into the Glyph.")
	SetEventText		(11)				// Tony face up-left
	Delay				(48)
	TextIcon			(ICON_GIA)
	TextWrite			("That must be=")
	         			("the key to")
	         			("unlocking the=")
	         			("Core Glyph's")
	         			("power!")
	TextIcon			(ICON_TONY)
	TextWrite			("But there are=")
	         			("three more")
	         			("slots. Where do=")
	         			("I get the other")
	         			("three stones?")
	SetEventText		(12)				// Gia look at Tony
	TextIcon			(ICON_GIA)
	TextWrite 			("Perhaps... now=")
	         			("that I know")
	         			("what we are=")
	         			("looking for, I")
	         			("can try to=")
	         			("locate them...")
	TextClose			()
	SetEventText		(13)				// Gia psychic powerz         			
	Delay				(96)
	TextOpen			()
	TextIcon			(ICON_GIA)
	TextWrite			("No good,=")
	         			("something is")
	         			("preventing me=")
	         			("from seeing it")
	         			("clearly. All I=")
	         			("sense is that")
	         			("they are all=")
	         			("concentrated in")
	         			("one area, yet=")
	         			("always moving.")
	         			("This can only=")
	         			("mean one thing:")
	         			("somebody is=")
	         			("holding them.")
	         			("No doubt it is=")
	         			("the same person")
	         			("who used this=")
	         			("stone to form")
	         			("the Geyser in=")
	         			("Naroom. Did you")
	         			("notice any=")
	         			("suspicious")
	         			("characters=")
	         			("around the")
	         			("Geyser?")
	TextIcon			(ICON_TONY)
	TextWrite 			("Actually, I did=")
	         			("notice someone,")
	         			("but he went too=")
	         			("fast for me to")
	         			("see clearly.")
	TextIcon			(ICON_GIA)
	TextWrite 			("Did you see=")
	         			("where he went?")
	TextClose			()
	
	If					([Blades_State = 0],_LOST_MORAG)
	SetXRAMByte			(Blades_State,2)

	TextOpen			()
	TextIcon			(ICON_TONY)
	TextWrite			("He went into a=")
						("cave.")
						("I tried to=")
	         			("follow him, but")
	         			("he disappeared.=")
	         			("I did find")
	         			("these crystal=")
	         			("blades, though.")
	TextIcon			(ICON_GIA)
	TextWrite			("Let me have a=")
						("look...")
	SetEventText		(14)			// Look at blades (face Tony)
	TextWrite 			("Those look like=")
						("the kind of")
						("blades used by=")
						("miners in the")
						("Underneath.=")
						("He must have")
	         			("taken an old=")
	         			("mine shaft...")
	         			("Eidon can fit=")
	         			("those blades")
	         			("into digging=")
	         			("gloves for you.")
	TextIcon			(ICON_TONY)
	TextWrite			("Thanks for your=")
	         			("help.")
	TextIcon			(ICON_GIA)
	TextWrite			("Good luck,")
	         			("Kyros!")
	TextClose			()
	JumpLocal			(_END)

_LOST_MORAG
	TextOpen			()
	TextIcon			(ICON_TONY)
	TextWrite 			("North of where=")
	         			("the Geyser was,")
	         			("but I didn't=")
	         			("follow him.")
	TextIcon			(ICON_GIA)
	TextWrite 			("Perhaps you=")
	         			("should")
	         			("investigate.")
	TextClose			()

_END
	ThatActorSetScript	(ACTOR00,?_GAMECOUNT_GIA)
	ThatActorSetScript	(HEROACTOR,?_HERO_STAND_LEFT_ANIM)
	ThatActorSetAI		(HEROACTOR,?HERO_AI)
	End					()	
/********************************//********************************/
// END BEAT NAROOM
/********************************//********************************/


/********************************//********************************/
// BACK FROM CALD STORY
/********************************//********************************/
?_WVEGIASHUT02_CALD_GEYSER
	SetXRAMBit				(GiaScene_CaldGeyser,TRUE)

	SceneNew				()
	LoadMap					(MAP_WVEGIASHUT02)
	LoadHotspots			(HS_WVEGIASHUT02)
	LoadTriggers			(TR_WVEGIASHUT02)
	LoadPalette				(PAL_HERO)
	ThatActorInit			(HEROACTOR,?ANIM_AI,9,6,15,0,_TONY,?_END)
	ThatActorInit			(ACTOR00,?TALKER_AI,8,4,15,0,_GIA,?_GAMECOUNT_GIA_TALKER)
	ThatActorInit			(ACTOR01,?ANIM_AI,10,4,15,34,_EIDON,?_END)
	LoadVRAMTiles			(34,BIT_GIA,0,0)
	LoadVRAMTiles			(42,BIT_EIDON,34,0)
	ThatActorStart			(HEROACTOR)
	ThatActorStart			(ACTOR00)
	ThatActorStart			(ACTOR01)
	HeroSetCamera			()
	SongStart				(SONGID_giahut)
	PalClearAnim			(0,15,31,31,31)
	SceneReady				()
	PalFade					(0,15,32,4)
	SetText					(_AMAZING_JOURNEY)
	End						()

/********************************/
// ACTORS
_TONY
	ThisActorSetAnim		(?_HERO_STAND_UL_ANIM)
	Sync					(3,3)
	ThisActorSetAnim		(?_HERO_STAND_UR_ANIM)
	Sync					(4,4)
	ThisActorSetAnimDelay	(?_HERO_STAND_UR_ANIM,32)
							(?_HERO_STAND_RIGHT_ANIM,16)
							(?_HERO_STAND_DOWN_ANIM,16)
							(?_HERO_STAND_UP_ANIM,0)
	End						()

_GIA
	ThisActorSetAnim		(?_GIA_STAND_DR_ANIM)
	Sync					(3,3)
	ThisActorSetAnim		(?_GIA_STAND_RIGHT_ANIM)
	Sync					(4,4)
	ThisActorSetAnimDelay	(?_GIA_STAND_RIGHT_ANIM,16)
							(?_GIA_STAND_DR_ANIM,16)
							(?_GIA_WALK_RIGHT_ANIM,16)		
							(?_GIA_STAND_DOWN_ANIM,0)		
	WaitEventText			(1)
	ThatActorSetAI			(HEROACTOR,?HERO_AI)
	ThatActorSetScript		(HEROACTOR,?_HERO_STAND_UP_ANIM)
	End						()

_EIDON
	ThisActorSetAnim		(?_EIDON_STAND_DL_ANIM)
	Sync					(2,0)	
	ThisActorSetAnim		(?_EIDON_STAND_LEFT_ANIM)
	Sync					(2,1)	
	ThisActorSetAnim		(?_EIDON_STAND_DL_ANIM)
	Sync					(2,2)	
	ThisActorSetAnim		(?_EIDON_STAND_LEFT_ANIM)
	Sync					(4,4)
	ThisActorSetAnimDelay	(?_EIDON_WALK_DOWN_ANIM,48)
							(?_EIDON_WALK_LEFT_ANIM,16)
							(?_EIDON_WALK_DOWN_ANIM,32)
	ThisActorStop			()
	End						()


/********************************/
// TEXT
_AMAZING_JOURNEY
	TextOpen			()
	TextIcon			(ICON_TONY)
	TextWrite			("..And then I=")
						("came here!")
	TextIcon			(ICON_GIA)
	Sync				(2,0)	
	TextWrite			("That is an=")
						("amazing journey")
						("Kyros!")
						("You are truly=")
						("living up to")
						("the legend...=")
						("Are you going")
						("back to the=")
						("Cald?")
	TextClear			()
	Sync				(2,1)	
	TextIcon			(ICON_TONY)
	TextWrite			(" Sure=")
						(" NO!$")
	TextMenu			(2)
						(_YES)
						(_NO)
_NO
	TextClear			()
	TextIcon			(ICON_GIA)
	TextWrite			("BUT THOU MUST!")
	TextIcon			(ICON_TONY)
	TextWrite			("Well....")
						("Okay.")
_YES
	TextIcon			(ICON_GIA)
	Sync				(2,2)	
	TextWrite			("Good!=")
						("Even the")
						("people of=")
						("the Cald")
						("don't deserve=")
						("one of those")
						("terrible=")
						("Geysers.")
	TextIcon			(ICON_EIDON)
	Sync				(3,3)
	TextWrite			("Just barely...")
	TextIcon			(ICON_GIA)
	TextWrite			("OK Eidon.")
						("That is a=")
						("long enough")
						("break.")
	TextIcon			(ICON_EIDON)
	Sync				(4,4)	
	TextWrite			("........")
	TextClose			()
	ThatActorSetAI		(HEROACTOR,?HERO_AI)
	ThatActorSetScript	(HEROACTOR,?_HERO_STAND_UP_ANIM)
	End					()
/********************************//********************************/
// END BACK FROM CALD STORY
/********************************//********************************/